plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.example.baseframe"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion  rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi-v7a', 'arm64-v8a'//, 'x86'
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            zipAlignEnabled true
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }

    dexOptions {
        javaMaxHeapSize "4g"
        jumboMode = true
        preDexLibraries = false
        additionalParameters = [
                '--multi-dex',//多分包
                '--set-max-idx-number=60000'//每个包内方法数上限
        ]
    }

    buildFeatures {
        dataBinding = true
    }

    //release版本输出包名自动追加版本号和版本名称
    applicationVariants.all { variant ->
        variant.outputs.all {
            //只处理生产版本
            if (buildType.name == 'release') {
                def createTime = new Date().format("yyyy-MM-dd HH-mm-ss", TimeZone.getTimeZone("GMT+08:00"))
                // app包名称
                outputFileName = "V" + defaultConfig.versionName + "_" + createTime + "_app-" + buildType.name + ".apk"
            }
        }
    }
}

dependencies {

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //androidX标准库
    implementation "androidx.appcompat:appcompat:$supportLibVersion"
    implementation "androidx.multidex:multidex:$multidex"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    //baselib
    implementation project(':ThirdLibrays:baselibrary')
    implementation project(':ThirdLibrays:videocompressor')

    //圆图、图片加载
    implementation "com.makeramen:roundedimageview:$roundedimageview"

    //切换域名
    implementation "me.jessyan:retrofit-url-manager:$retrofitUrlManager"
    //腾讯web
    implementation "com.tencent.tbs.tbssdk:sdk:$tbssdk"
    //权限请求
    implementation "com.github.getActivity:XXPermissions:$xxPermissions"
    //图片选择
    implementation "io.github.lucksiege:pictureselector:$pictureselector"
    //hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    //提高启动速度
    implementation "androidx.startup:startup-runtime:$startup"
    //tab
    implementation "com.flyco.tablayout:FlycoTabLayout_Lib:$flycoTabLayout_Lib"
    //banner
    implementation "io.github.youth5201314:banner:$banner"
    //定位
    implementation "com.amap.api:location:latest.integration"
    //缩放图片
    implementation "com.github.chrisbanes:PhotoView:$PhotoView"

}